PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX urb: <http://purl.archive.org/urbooks#>
PREFIX urw: <http://purl.archive.org/urwriters#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dul: <http://www.ontologydesignpatterns.org/ont/dul/DUL.owl#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

CONSTRUCT {
  ?edition a urb:Edition;
  	rdfs:label ?titolo;
	  frbr:isEmbodimentOf ?author .

  ?exp a frbr:Expression;
      prov:wasAttributedTo ?author .

  ?pub a frbr:Publication;
     urb:hasParticipant ?edition, ?author, ?translator, urb:author, urb:translator ;
     prov:wasDerivedFrom ?source .

  ?author a urw:Person;
      rdfs:label ?nomeautore;
      dul:role urb:author .

  ?translator a ?translatortype;
      rdfs:label ?translatorlabel;
      dul:role urb:translator .

  ?source schema:url ?url .

}
WHERE {
    SERVICE <x-sparql-anything:csv.headers=true,location=./data/works_role_data.csv>
    {
      [] xyz:autore ?autore ;
            xyz:edizione ?edizione ;
            xyz:idpub ?id;
            xyz:nomeautore ?nomeautore;
            xyz:nometraduttore ?nometraduttore;
            xyz:titolo ?titolo;
            xyz:traduttore ?traduttore;
            xyz:idAutore ?idautore;
          xyz:url ?url .

    }
    BIND (IRI(CONCAT(STR(urb:), ?edizione)) AS ?edition ) .
    BIND (IRI(CONCAT(STR(urb:), ?idautore)) AS ?exp ) . 
    BIND (IRI(CONCAT(STR(urb:), ?id)) AS ?pub ) .
    BIND (IRI(CONCAT(STR(urw:), ?autore)) AS ?author ) .
    BIND (IRI(CONCAT(STR(schema:), "ibs")) AS ?source ) .

    # Label del traduttore predefinito se non disponibile
    BIND (IF(STRLEN(?nometraduttore) > 0, ?nometraduttore, "Traduttore inesistente") AS ?translatorlabel) .
    
    # IRI del traduttore predefinito se manca il codice del traduttore
    BIND (IF(STRLEN(?traduttore) > 0, IRI(CONCAT(STR(urw:), ?traduttore)), urw:translator_inesistente) AS ?translator) .

    # Condizione per definire dinamicamente il tipo del translator
    BIND (IF(STRLEN(?traduttore) > 0, urw:Person, urb:NonIdentifiedTranslator) AS ?translatortype)
     
}